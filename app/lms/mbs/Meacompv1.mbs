!******************** meacompv1 **********************
! +++
! Creates a composite (straight lines) deviation curve
!
! ---
! Revision history
! ________________
!
! 1993-11-05 Gunnar Liden
! 1995-03-09 Dokumentation
! 1995-04-11 R Gyllensten
! 1996-03-19 meacompv0->meacompv1 (CUR_CONARR) G. Liden
! 1997-04-25 Documentation in English          G. Liden
!
!*********************************************************

!sdescs Funktionen skapar en sammansatt felkurva av räta linjer
!sdesce Creates a composite (straight lines) deviation curve

LOCAL GEOMETRY MODULE meacompv1  (

     FLOAT  transl_x;             ! Translation for X axel values 
     FLOAT  skalf_x;              ! Scale factor  for X axel values 
     FLOAT  skalf_y;              ! Scale factor  for Y      values 
     INT    grafik;               ! =1: Visa allt =2: Blanka allt 
     INT    n_p;                  ! Number of points in subset (in arrays)
VAR  VECTOR m_points(200) ;       ! Measured  points 
VAR  VECTOR r_points(200) ;       ! Reference points 
VAR  FLOAT  devtot(200) ;         ! Total (perpendic.) deviation 
VAR  INT    ptnumber(200) ;       ! Point number for measured point
VAR  FLOAT  x_ut(200));           ! Scaled X coordinates


! Internal variables:


  INT       i_p;                  ! Loop index point in curve
  INT       blank_flag;           ! Flag for BLANK


  VECTOR    p(200);               ! Definition points  
  VECTOR    tp(200);              ! Tangent definition points  
  STRING    mf(200)*1;            ! Flag for "P" or "M"
  FLOAT     pval(200);            ! P values= 0 for straight line
  VECTOR    pm(200);              ! Mid point

  STRING    s*1;                  ! String for DEBUG (INPMT)
  INT       DEBUG;                ! For Debug

BEGINMODULE

! +++
! Algorithm
! ________
!
! 1. Initializations and checks 
! ---

  IF  grafik = 2 THEN                                ! Blank flag
    blank_flag := 1;                                 !
    DEBUG := 0;                                      ! No Debug 
  ELSE                                               !
    blank_flag := 0;                                 !
    DEBUG := 1;                                      ! Debug On
  ENDIF;                                             !

  FOR i_p := 1 TO 200 DO           ! Initialization to make something work ..
   x_ut(i_p):= 100000.0;           ! 
  ENDFOR;                          !
                                   ! (ugly programming !        )

  IF  n_p > 200 THEN                                 ! Check number of input points
    EXIT("meacompv1 n_p > 200");                     !
  ENDIF;                                             !

! +++
! 2. Arrays with scaled point data
! ---


    FOR i_p := 1 TO n_p   DO                         ! Start loop all pts

    x_ut(i_p):=                                      ! Scaled X value for pt
    (m_points(i_p).x-transl_x+1.0)                   !
     *skalf_x;                                       !

    p(i_p)   := VEC(x_ut(i_p),devtot(i_p)*skalf_y,0);! Definition pt
    tp(i_p)  := VEC(0,0,0);                          ! Tangent pt  
    mf(i_p)  := "P";                                 ! Pvalue flag
    pval(i_p):= 0.0;                                 ! Straight line
    pm(i_p)  := VEC(0,0,0);                          ! Mid point

    ENDFOR;                                          ! End  loop all pts


! +++
! 3. Create the composite straight line curve
! ---

  CUR_CONARR(#1,"FREE",n_p-1,                        ! Create a composite conic
     p,tp,mf,pval,pm                                 !
     :BLANK=blank_flag);                             !


ENDMODULE
