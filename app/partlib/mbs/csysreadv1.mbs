!******************** csysreadv1 ***************************************
! +++
! Reads coordinate system data from text file .
!
! Revision history
! ________________
!
! 1996-04-25 (Source csysreadv0) Karl-Erik Eklund
!
! 1997-01-22  Karl-Erik Eklund   origin corrected and c_data(4,*) added 
! 
! ---
!***********************************************************************
!sdesce Reads coordinate system data from text file .
!sdescs Läser koordinatsystem data från fil            

BASIC GEOMETRY MODULE csysreadv1 (
    STRING c_name*132;           ! Full file name
VAR FLOAT  c_data(4,4);          ! Coordinate system data
VAR VECTOR origin;               ! Origin for system
VAR STRING benämning*132);       ! Description in swedish for the coordinate system

! Internal variables:

  STRING      csys_part*28;    ! Koordinatsystemsnamn läst
                               ! från part-data med GETSTR

  FILE        f;               ! File with data  (F07-file)

  STRING      rad*132;         ! Dummy variable for read

  STRING      s*1;             ! Sträng för DEBUG (INPMT)
  INT         DEBUG;           ! För kontroll utskrifter

BEGINMODULE

! +++
! Algorithm
! _________
! ----

! Initializations


  DEBUG := 0;                  ! Change to 0 or 1

! +++
! Open file
! ---

   OPEN(f, "r", c_name );       ! Open

   IF IOSTAT(f) <> 0 THEN       ! Error for no file
     EXIT("csysreadv1 No file " !
      + c_name );               !
   ENDIF;

   rad:= INLIN(f);              ! Läser förbi första raden (namnet är kännt)
   IF DEBUG = 1 THEN
     s:= INPMT("csysreadv1: rad1= "+rad ,"y",1);
   ENDIF;
   
   rad:= INLIN(f);              ! Läser förbi andra raden (proj.-namnet 
                                ! i COGNAC saknar intresse här)
   IF DEBUG = 1 THEN
     s:= INPMT("csysreadv1: rad2= "+rad ,"y",1);
   ENDIF;
   
   benämning:= INSTR(f);        ! Läser benämningen på koordinatsystemet                                   
   IF DEBUG = 1 THEN
     s:= INPMT("csysreadv1: benämning= "+benämning ,"y",1);
   ENDIF;
   rad:= INLIN(f);              ! Läser till ny rad
   IF DEBUG = 1 THEN
     s:= INPMT("csysreadv1: rad3 till slut= "+rad ,"y",1);
   ENDIF;
   
   rad:= INLIN(f);              ! Läser förbi fjärde raden 
   IF DEBUG = 1 THEN
     s:= INPMT("csysreadv1: rad4= "+rad ,"y",1);
   ENDIF;
   
   c_data(1,1):= INFLT(f);      !
   IF DEBUG = 1 THEN
     s:= INPMT("csysreadv1: c_data(1,1)= "+STR(c_data(1,1),12,8),"y",1);
   ENDIF;
   
   c_data(1,2):= INFLT(f);      !
   IF DEBUG = 1 THEN
     s:= INPMT("csysreadv1: c_data(1,2)= "+STR(c_data(1,2),12,8),"y",1);
   ENDIF;
   
   c_data(1,3):= INFLT(f);      !
   IF DEBUG = 1 THEN
     s:= INPMT("csysreadv1: c_data(1,3)= "+STR(c_data(1,3),12,8),"y",1);
   ENDIF;
   
   rad:= INLIN(f);              ! Läser till ny rad

   c_data(2,1):= INFLT(f);      !
   c_data(2,2):= INFLT(f);      !
   c_data(2,3):= INFLT(f);      !
   rad:= INLIN(f);              ! Läser till ny rad

   c_data(3,1):= INFLT(f);      !
   c_data(3,2):= INFLT(f);      !
   c_data(3,3):= INFLT(f);      !
   rad:= INLIN(f);              ! Läser till ny rad

   c_data(1,4):= INFLT(f);      !
   c_data(2,4):= INFLT(f);      !
   c_data(3,4):= INFLT(f);      !
   rad:= INLIN(f);              ! Läser till ny rad


   origin.x:= - c_data(1,1)*c_data(1,4)
              - c_data(2,1)*c_data(2,4)
              - c_data(3,1)*c_data(3,4);
              
   IF DEBUG = 1 THEN
     s:= INPMT("csysreadv1: origin.x= "+STR(origin.x,12,8),"y",1);
   ENDIF;
   
   origin.y:= - c_data(1,2)*c_data(1,4)
              - c_data(2,2)*c_data(2,4)
              - c_data(3,2)*c_data(3,4);
              
   IF DEBUG = 1 THEN
     s:= INPMT("csysreadv1: origin.y= "+STR(origin.y,12,8),"y",1);
   ENDIF;
   
   origin.z:= - c_data(1,3)*c_data(1,4)
              - c_data(2,3)*c_data(2,4)
              - c_data(3,3)*c_data(3,4);
              
   IF DEBUG = 1 THEN
     s:= INPMT("csysreadv1: origin.z= "+STR(origin.z,12,8),"y",1);
   ENDIF;
   
   
! +++
! Close file
! ---

   CLOSE(f);                    ! Close

   c_data(4,1):= 0;
   c_data(4,2):= 0;
   c_data(4,3):= 0;
   c_data(4,4):= 1;
   
ENDMODULE

