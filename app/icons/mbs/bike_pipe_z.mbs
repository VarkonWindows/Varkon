! ***************** bike_pipe_z ****************************
!
! +++
! Create pipe around input spine curve, i.e. profile= circle
!
!
! 2022-12-28 Gunnar Liden
! ---

!************************************************************

!sdesce Create pipe around input spine curve

GLOBAL GEOMETRY MODULE bike_pipe_z(
REF     id_spine     := #0   > "@t10 Spine";
FLOAT   diameter     := 20.0 > "Diameter";
INT	    reverse      := -1   > "Reverse for SUR_SWEEP";
INT	    sur_color    :=  3   > "Surface color";
INT     graphic      :=  1   > "Graphic flag");


CONSTANT VECTOR y_dir = VEC(1.0, 0.0, 0.0);

INT    b_val;              ! Blank value

BEGINMODULE

  IF   graphic = 0 THEN
    b_val := 1;
  ELIF graphic = 1 THEN
     b_val := 0;
  ELIF graphic = 2 THEN
     b_val := 0;
  ELSE
    EXIT("bike_pipe_z Graphic flag not 0, 1 or 2");
  ENDIF;
  
! Profile

  MODE_BASIC();

  ARC_1POS(#2, vec(0,0,0), 0.5*diameter:PEN=sur_color, BLANK= 1);

  MODE_GLOBAL();


! Surface

  SUR_SWEEP(#3, #2, id_spine, reverse, y_dir:BLANK=1, PEN=3);

  SUR_APPROX(#1,#3, "CUB_SUR", 1, 4:PEN=sur_color, BLANK= b_val);
  
  DEL(#3);

ENDMODULE

 