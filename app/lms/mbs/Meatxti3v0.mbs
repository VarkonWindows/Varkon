!******************* meatxti3v0*********************************************
! +++
! Text (surface, description, .. ) for drawing sheet mearami3v0 
!
! ---
! Revision history
! ________________
!
! 1993-09-17 Gun-Britt Ekros
! 1993-12-15 Ref-textens storlek justeras med modtsize Ronny Gyllensten
! 1996-03-04 BASIC->GLOBAL, Documentation, }{|  Gunnar Liden
! 1996-03-18 Documentation, +++ och ---  Gun-Britt Ekros 
! 1996-03-25 Default values              Gunnar Liden
! 1996-04-21 Special version IPK         Gunnar Liden
! 1997-04-24 Special version LMS         Gunnar Liden
!
!************************************************************************
!sdesce Text (surface, description, .. ) for measurement drawing sheet 
!sdescs Text (yta, ben{mning, ...) f|r m{tanalys ritblankett

GLOBAL DRAWING MODULE meatxti3v0 (
STRING s_d*132   :="    FLAP 2000"              >"Surface description";
STRING f_txt*132 :="VARKON application MEASURE" >"Free text";
STRING projyt*132:="FLAP_DEMO"                  >"Surface name";
STRING bladnr*132:="Page 1"                     >"Page number";
STRING datum*132 :="1997-04-24"                 >"Date";
STRING dept*132  :="LMS"                        >"Department";
STRING ref1*132  :="Ref: File name .."          >"Reference";
STRING modul*132 :="VARKON module ..."          >"VARKON module name";
INT    text_pen  :=  2                          >"Colour for text";
INT    text_niva := 12                          >"Level for text");

!  Internal variables:

   FLOAT  faltbredd;                      ! Available space for text
   FLOAT  inhojd;                         ! Input size (mm) for text
   FLOAT  inbredd;                        ! Input width (%) for text 
   FLOAT  uthojd;                         ! Modified size  for text
   FLOAT  utbredd;                        ! Modified width for text
   FLOAT  alfa;                           ! Angle for text

BEGINMODULE


  SET(LEVEL=text_niva);                        ! Level  for text 
  SET(PEN  =text_pen);                         ! Colour for text
  SET(TSIZE=4.0);                              ! Text size  (mm)
  SET(TWIDTH=50);                              ! Text width (%)
  alfa:= 0.0;                                  ! Angle for text   

  faltbredd:=116;                              ! Available space
  inhojd:=4.0;                                 ! Input text size
  inbredd:=50;                                 ! Input text width
  PART(#50,modtsize(s_d      ,faltbredd,       ! Procedure which modifies
    inhojd,inbredd,uthojd,utbredd):SAVE=0);    ! text size and width for
                                               ! the available space
  TEXT(#2 ,vec(213, 262), alfa, s_d            ! Write surface description with
           :TSIZE=uthojd,TWIDTH=utbredd);      ! the calculated size and width
                                               ! #2 is the identity for the text
                                               ! vec(x,y)= position for text 
                                               ! s_d is the text    
  
  faltbredd :=116;                             ! Available space
  inhojd    :=4.0;                             ! Input text size
  inbredd   :=50;                              ! Input text width
  PART(#51,modtsize(f_txt      ,faltbredd,     ! Calculates new size
    inhojd,inbredd,uthojd,utbredd):SAVE=0);    ! and width if faltbredd
                                               ! is too small
  TEXT(#4 ,vec(213, 252), alfa, f_txt          ! Write text on drawing 
           :TSIZE=uthojd,TWIDTH=utbredd);      ! 


  faltbredd:=49;                               !
  inhojd:=4.0;                                 !
  inbredd:=50;                                 !
  PART(#53,modtsize(projyt,faltbredd,          !
    inhojd,inbredd,uthojd,utbredd):SAVE=0);    !
  TEXT(#8 ,vec(337, 262), 0.0, projyt          ! Write surface name
           :TSIZE=uthojd,TWIDTH=utbredd);      !

  TEXT(#10 ,vec(361, 272), alfa , bladnr );    ! Write page number 

  SET(TSIZE=2);                                ! Text size      
  SET(TWIDTH=50);                              ! Text width  

  TEXT(#12 ,vec(176, 265), 0.0, datum );       ! Write date
 
  TEXT(#14 ,vec(176, 262), 0.0, dept     );    ! Write department

  faltbredd:=168;                              !
  inhojd:=3.0;                                 !
  inbredd:=50;                                 !
  PART(#52,modtsize(ref1,faltbredd,            ! 
    inhojd,inbredd,uthojd,utbredd):SAVE=0);    !

  TEXT(#24 ,vec(5, 263), alfa, ref1            ! Write reference
           :TSIZE=uthojd,TWIDTH=utbredd);      ! 

  SET(TSIZE=3);                                ! Text size    
  SET(TWIDTH=50);                              ! Text width  

  alfa:= 90.0;                                 ! Angle for text   
  TEXT(#40,vec( -2,  12), alfa, modul );       ! Write module name 

ENDMODULE
