!**************** su_cappr   ***************************
!s+++
! CUR_APPROX
! ________
!
! Bildar en ny kurva genom approximation av en tidigare skapad  
! kurva. Funktionen kan till exempel användas för att approximera
! en UV-kurva (en kurva på en yta) med en kurva enbart bestående
! av kubiska segment.
! 
! Modultyp: DRAWING och GEOMETRY
!
! Anrop: CUR_APPROX(identitet,kurva,typ,metod,extra1,extra2 :attribut)
!
! Obligatoriska parametrar:
!
! kurva   (REF)    - Tidigare skapad kurva
!
! typ     (STRING) - Önskad typ av segment i kurvan
!                    CUB_SEG/CUBIC eller RAT_SEG/RATIONAL
! metod   (INT)    - Approximationsmetod
!                    metod = 1: Antal nya segment är indata         
!                    metod = 2: Segment får bara läggas till        
! extra1  (FLOAT)  - Extra data för beräkningen
!                    extra1 = Önskat antal segment för metod= 1
!                    extra1 = Koordinat tolerans   för metod= 2
!
! Optionella parameterar:   
!
! extra2  (FLOAT)  - Extra data för beräkningen
!                    extra2 = Tangent (vinkel) tolerans för metod= 2
!
! Attribut: PEN, LEVEL, BLANK, CFONT och CDASHL
!
! Exempel:
! 
! CUR_APPROX( #10, #5, "CUBIC", 1, 4 );
! 
! Skapar en ny kurva (#10) som approximerar en tidigare skapad       
! kurva #5. Antalet segment blir fyra gånger det ursprungliga
! antalet segment i kurvan.
!s---
!e+++
! CUR_APPROX
! ________
!
! The function creates an approximated curve. Input is a      
! previously defined curve. 
!
! The function can for instance be used to approximate a UV curve
! (a curve on a surface) with a curve with cubic segments.
!
! Module type: DRAWING and GEOMETRY
!
! MBS syntax: CUR_APPROX(identity,curve,typ,metod,extra1,extra2 :attribut)
!
! Parameters:
!
! curve   (REF)    - Previously defined curve  
!
! typ     (STRING) - Requested type of segments in the curve
!                    CUB_SEG/CUBIC or RAT_SEG/RATIONAL
! metod   (INT)    - Approximation method
!                    metod = 1: Number of segments is input         
!                    metod = 2: Add only segments                   
! extra1  (FLOAT)  - Extra data for the computation
!                    extra1 = Requested number of segments for metod= 1
!                    extra1 = Coordinate tolerance   for metod= 2
!
! Optionella parameterar:   
!
! extra2  (FLOAT)  - Extra data for the computation
!                    extra2 = Tangent (angle) tolerance for metod= 2
!
! Attribute: PEN, LEVEL, BLANK, CFONT and CDASHL
!
! Example:
! 
! CUR_APPROX( #10, #5, "CUBIC", 1, 4 );
! 
! Creates a new curve (#10) which approximates a previously   
! created curve #5 with cubic segments. The number of segments
! will be four times the number of segments for the old curve.
!e---

!sdesce Add CUR_APPROX statement to active module (approximate a curve)
!sdescs Addera CUR_APPROX sats till aktiva modulen (approximera en kurva)    

! Revision history
! ________________
!
! 1995-12-31 Originally written    Gunnar Liden
! 1996-03-23 ma_curappr->su_cappr  Gunnar Liden
! 1996-10-16 metod= 2 added        Gunnar Liden

MACRO MODULE su_cappr   ();

CONSTANT INT language= 2;        ! 1: English Eq. 2: Swedish

! Internal variables:

  REF    c_id;                   ! Surface identity
  STRING c_ids*132;              ! Surface identity as string

  STRING select*30;              ! Select prompt string

  STRING p_typ*80;               ! Type of curve  prompt string
  STRING p_met*80;               ! Approx. method prompt string
  STRING p_e1_n*80;              ! Extra data 1   prompt string
  STRING p_e1_t*80;              ! 
  STRING p_e2_n*80;              ! Extra data 2   prompt string
  STRING p_e2_t*80;              ! 
  STRING s_typ*80;               ! Type of curve 
  STRING s_met*80;               ! Approx. method as string
  STRING s_e1*25;                ! Extra data 1   as string
  STRING s_e2*25;                ! Extra data 2   as string

  STRING str_in*132;             ! Input string to CUR_GL

  REF    a_id;                   ! ADD_MBS identity

BEGINMODULE 

! 1. Definition of prompt strings
! _______________________________

IF   language = 1 THEN           ! English strings
  select:= "Select curve";       !
  p_typ:="Requested type "+      !
    "(CUBSEG, RATSEG ..)";       !
  p_met:="Approximation method "+!
  "(=1: No. of segm.'s =2: Tolerances )";!
  p_e1_n:="Number of segments";          !
  p_e2_n:="Undefined    ";         !
  p_e1_t:="Coordinate tolerance";          !
  p_e2_t:="Tangent angle tolerance";         !
ELIF language = 2 THEN           ! Swedish strings
  select:= "Peka på kurva";      !
  p_typ:="Önskad typ "+          !
    "(CUBSEG, RATSEG ..)";       !
  p_met:="Approximations metod "+!
  "(=1: Ge antal =2: Ge toleranser)";!
  p_e1_n:="Antal segment ";        !
  p_e2_n:="Ej definierat ";        !
  p_e1_t:="Koordinat tolerans";          !
  p_e2_t:="Tangent vinkel tolerans";         !
ELSE                             !
  EXIT("su_cappr  : " +          !
  "Language error");             !
ENDIF;                           !

! 2. Select curve  
! _________________

PSH_PMT(select);                 ! Select string to screen
c_id:= IDENT(2+4+8);             ! Interactive curve selection
POP_PMT();                       ! Erase message to screen

c_ids:= RSTR(c_id);              ! Surface identity as string
IF c_ids  = "#0.1" THEN          !
  EXIT();                        !
ENDIF;                           !

c_ids:= "GLOBAL_REF("+c_ids+")"; ! Global reference

! 3. Approximation data
! ______________________

s_typ:= INPMT(p_typ,             ! Interactive input of curve type
    "CUB_SEG",25);               !
IF s_typ   = "" THEN             !
      EXIT();                    !
ENDIF;                           !
s_typ:= CHR(34)+s_typ+CHR(34);   !

s_met:= INPMT(p_met,             ! Interactive input of method      
    "1",      25);               !
IF s_met   = "" THEN             !
      EXIT();                    !
ENDIF;                           !

IF   s_met = "1" THEN            !
  s_e1 := INPMT(p_e1_n,          ! Interactive input of extra data 1
    "4",   25);                  !
ELSE                             !
  s_e1 := INPMT(p_e1_t,          ! 
    "0.05",25);                  !
ENDIF;                           !
IF s_e1    = "" THEN             !
      EXIT();                    !
ENDIF;                           !

IF s_met = "1" THEN              !
  s_e2 := "-0.123456789";        !
ELSE                             !
  s_e2 := INPMT(p_e2_t,          ! Interactive input of extra data 2
    "0.1", 25);                  !
ENDIF;                           !
IF s_e2    = "" THEN             !
      EXIT();                    !
ENDIF;                           !

! 4. Create MBS statement
! _______________________

IF s_met = "1" THEN              !
  a_id:= ADD_MBS("CUR_APPROX",   ! Create MBS statement 
     c_ids, s_typ, s_met, s_e1); !
ELSE                             !
  a_id:= ADD_MBS("CUR_APPROX",   ! 
     c_ids, s_typ, s_met, s_e1,  !
     s_e2 );                     !
ENDIF;                           !

ENDMODULE 

