! ***************** su_visop    ****************************
!
! +++
!e+++
! su_visop
! ________
!
! Create V isoparameter curves on a surface
!
!e---
!s+++
! su_visop
! ________
!
! Skapa ett knippe V isoparameterkurvor pa en yta
!
!s---
!
! Revision history
! ________________
!
! 1995-08-30 Gunnar Liden
! 1996-03-01 delta bug           Gunnar Liden
! 1996-03-23 s_isovpar->su_visop Gunnar Liden
!
! ---
!************************************************************

!sdesce Create V isoparameter curves on a surface
!sdescs Skapa ett knippe V isoparameterkurvor p} en yta

GLOBAL GEOMETRY MODULE su_visop    (

REF    s_id                  > "@t16 Peka p} en yta";
INT    n_v         := 30     > "Ange antal V parameter kurvor (minst 2)");


! Internal variables

  INT    npatu;                  ! No patches in U direction
  INT    npatv;                  ! No patches in V direction

  STRING iso_c*1;                ! Isoparameter U or V
  FLOAT  start_p;                ! Parameter for start point
  FLOAT  end_p;                  ! Parameter for end   point

  INT    i_v;                    ! Loop index V parameters

  FLOAT  delta;                  ! Delta parameter value

BEGINMODULE


!e+++
! Algorithm
! _________
!e---
!s+++
! Algoritm
! ________
!s---

!e+++
! Retrieve number of patches. Calls of GETSURH.
!e---
!s+++
! Ta reda p} antal "patches" i ytan. Anrop av GETSURH.
!s---


  GETSURH(s_id,"NPATU",npatu);   ! Retrieve number of U patches
  GETSURH(s_id,"NPATV",npatv);   ! Retrieve number of V patches

!e+++
! Create V parameter lines. Calls of CUR_ISO.
!e---
!s+++
! Skapa V parameter kurvor. Anrop av CUR_ISO.
!s---

  IF n_v >= 2 THEN               ! Delta parameter
    delta:= npatv/(n_v-1);       !
  ELSE                           !
    EXIT("su_visop  n_v < 2");   !
  ENDIF;

  iso_c  := "V";                 ! Type of isoparameter
  start_p:=   0.0;               ! Start value 
  end_p  :=   npatu;             ! End   value

  FOR i_v:= 1 TO  n_v  DO        ! Start loop U curves

    CUR_ISO(#1,s_id,iso_c,       ! Isoparameter
        (i_v-1)*delta,               !
        start_p,end_p);          !

  ENDFOR;                        ! End   loop U curves    


ENDMODULE
